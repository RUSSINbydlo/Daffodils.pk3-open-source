actor ColtSA : Weapon 27089
{
  Weapon.SelectionOrder 1900
  Weapon.AmmoType1 "ColtMag"
  Weapon.AmmoType2 "Clip"
  Weapon.AmmoUse1 1
  Weapon.AmmoGive 60
  Weapon.SlotNumber 2
  Weapon.SlotPriority 0.9
  +WEAPON.AMMO_OPTIONAL
  Obituary "%o was Killed by %k's Revolver."
  Inventory.Pickupmessage "Picked up a Revolver, hell yeah"
  States
  {
  Ready:
	COLT A 1 A_WeaponReady(WRF_NOSECONDARY|WRF_ALLOWRELOAD)
    Loop
  Uncock:
	COLT B 1 A_WeaponReady(WRF_NOPRIMARY|WRF_ALLOWRELOAD)
	Loop
  Deselect:
	COLT AADDE 1 A_Lower(12)
	Goto Deselect+4
  Select:
	COLT EEEEEED 1 A_Raise(12)
	COLT D 0 A_StartSound("COLTCLAC")
	COLT D 1 A_Raise(12)
	Goto Select+8
  Reload:
	COLT F 0 A_Jumpifinventory("ColtMag",6,"Ready")
	COLT F 0 A_Jumpif(countInv("Clip")<2 ,"NoReload")
	COLT F 1 A_WeaponOffset(-2,32)
	COLT F 1 A_WeaponOffset(-4,32)
	COLT F 1 A_WeaponOffset(-6,32)
	COLT G 0 A_StartSound("ColtOpen",0)
	COLT G 1 A_WeaponOffset(-4,32)
	COLT G 1 A_WeaponOffset(-2,32)
	COLT H 1 A_WeaponOffset(-1,32)
	COLT H 1 A_WeaponOffset(0,32)
	COLT H 6
	COLT H 1 A_WeaponOffset(20,52,WOF_INTERPOLATE)
	COLT H 0 A_StartSound("ColtEjec",0)
	COLT H 1 A_WeaponOffset(40,72,WOF_INTERPOLATE)
	COLT M 0 A_Jump(256,(1+CountInv("Coltmag")))
	COLT M 0 A_FireProjectile("RevlCase",-80+Random(-2,2),0,0,0,FPF_NOAUTOAIM,-24+Random(-14,14))
	COLT M 0 A_FireProjectile("RevlCase",-80+Random(-2,2),0,0,0,FPF_NOAUTOAIM,-24+Random(-14,14))
	COLT M 0 A_FireProjectile("RevlCase",-80+Random(-2,2),0,0,0,FPF_NOAUTOAIM,-24+Random(-14,14))
	COLT M 0 A_FireProjectile("RevlCase",-80+Random(-2,2),0,0,0,FPF_NOAUTOAIM,-24+Random(-14,14))
	COLT M 0 A_FireProjectile("RevlCase",-80+Random(-2,2),0,0,0,FPF_NOAUTOAIM,-24+Random(-14,14))
	COLT M 0 A_FireProjectile("RevlCase",-80+Random(-2,2),0,0,0,FPF_NOAUTOAIM,-24+Random(-14,14))
	COLT M 0
	COLT M 1 A_WeaponOffset(30,72)
	COLT M 1 A_WeaponOffset(18,50,WOF_INTERPOLATE)
	COLT M 1 A_WeaponOffset(9,41,WOF_INTERPOLATE)
	COLT M 1 A_WeaponOffset(3,35,WOF_INTERPOLATE)
	COLT M 3 A_WeaponOffset(0,32,WOF_INTERPOLATE)
	COLT L 3
	Relodin:
	COLT K 0 A_Jumpif(countInv("Clip")<2 ,4)
	COLT K 0 A_Jumpifinventory("ColtMag",6,3)
	COLT K 0 A_GiveInventory("ColtMag",1)
	COLT K 0 A_TakeInventory("Clip",2)
	Loop
	COLT K 4 A_StartSound("ColtInsr",0)
	COLT JI 3
	COLT H 1 A_WeaponOffset(2,32)
	COLT H 1 A_WeaponOffset(5,32)
	COLT H 1 A_WeaponOffset(8,32)
	COLT G 1 A_WeaponOffset(5,32)
	COLT G 0 A_StartSound("ColtClos",6)
	COLT G 1 A_WeaponOffset(2,32)
	COLT F 2 A_WeaponOffset(0,32)
	COLT B 2
	COLT A 0 A_StartSound("COLTCLAC",3)
	COLT A 1 A_Overlay(6,"Fanin")
    Goto Ready
  AltFire:
	COLT A 0 A_StartSound("COLTCLAC",3)
	alfre:
	COLT A 1 A_Overlay(6,"Fanin") 
	COLT A 0 A_Refire("alfre")
    Goto Ready
  NoReload:
	COLT A 0 A_StartSound("COLTCLAC",3)
	COLT A 0 A_Overlay(6,"Fanin")
	COLT A 10 A_WeaponReady(WRF_NOSECONDARY)
    Goto Ready
  Fire:
	COLT B 0 A_Jumpif(countInv("ColtMag")<=0 ,"DRY")
    COLT B 1
	{
	 A_RailAttack(36,0,1,none,"",RGF_SILENT,0,"ColtPuff",0,0,0,8,10,6,"Coltracer",4,90,2);
	 A_SetPitch(Pitch-frandom(5,6),SPF_INTERPOLATE);
	 A_Setangle(Angle+frandom(-5,5),SPF_INTERPOLATE);
	 A_StartSound("COLTPLOW",1);
	 A_Overlay(-1,"Flash");
	 A_WeaponOffset(0,44);
	}
	COLT B 1 A_WeaponOffset(0,37,WOF_INTERPOLATE)
	COLT E 1 A_WeaponOffset(0,32,WOF_INTERPOLATE)
    COLT E 3 A_WeaponReady(WRF_NOPRIMARY)
	COLT D 2 A_WeaponReady(WRF_NOPRIMARY)
    Goto Uncock
  Dry:
	COLT B 3 A_StartSound("COLTDRY",3)
	Goto Uncock
  Flash:
	COLT C 1 BRIGHT A_Light2
	COLT C 1 BRIGHT A_Light1
	TNT1 A 1 A_Light0
	Stop
  Fanin:
	FANR A 1
	FANR A 1 A_OverlayOffset(6,3,10,WOF_INTERPOLATE)
	FANR A 1 A_OverlayOffset(6,6,20,WOF_INTERPOLATE)
	FANR A 1 A_OverlayOffset(6,12,30,WOF_INTERPOLATE)
	FANR A 1 A_OverlayOffset(6,15,40,WOF_INTERPOLATE)
	FANR A 1 A_OverlayOffset(6,18,50,WOF_INTERPOLATE)
	FANR A 1 A_OverlayOffset(6,21,60,WOF_INTERPOLATE)
	Stop
  Spawn:
    COLP A -1
    Stop
  }
}
actor Coltmag : Ammo
{
Inventory.Amount 1
Inventory.MaxAmount 60
Inventory.Icon "RVCAB0"
States
	{
	Spawn:
	  TNT1 A -1
	Loop
	}
}

ACTOR ColtPuff : BulletPuff
{
Decal BulletChip
+FORCEDECAL
+ALwayspuff
}
ACTOR Coltracer
{
Height 1
+FLATSPRITE
+NoGravity
RenderStyle "Translucent"
alpha 0.6
YScale 10
xScale 0.5
States
 {
	Spawn:
	LZBM D 1 BRIGHT
	LZBM D 0 A_Jump(256,1,2,3)
	LZBM D 1
	LZBM D 1 A_setscale(0.45,10)
	LZBM D 1 A_setscale(0.4,10)
	LZBM D 1 A_setscale(0.34,10)
	LZBM D 1 A_setscale(0.3,10)
	LZBM D 1 A_setscale(0.2,10)
	LZBM D 1 A_setscale(0.1,10)
	Stop
 }
}
ACTOR RevlCase
{
	Mass 1
	Scale 0.25
	Speed 5
	Radius 2
	Height 4
	+Missile
	+BOUNCEONACTORS
	+ROLLSPRITE
	+ROLLCENTER
	BounceSound "SHBOUNS1"
	BounceType "Doom"
	BounceFactor 0.5
	States
	{
		Spawn:
		RVCA A 2 A_SetRoll(roll+45)
		Loop
		Death:
		RVCA A 400 A_SetRoll(Randompick(90,270))
		Fade:
		RVCA A 1 A_FadeOut(0.02)
		Loop
	}
}


//statue lol
ACTOR DiscipleStatue1 15740
{
	//$Title Disciple Statue Stone
	//$Category Obstacles/Disciple Statues
	//$NotAngled
	//$IsDecoration
	
	Radius 10
	Height 76
	Scale 0.7
	+SOLID
	
		States
		{
			Spawn:
				DISS A -1
				Stop
		}
}

ACTOR DiscipleStatue2 : DiscipleStatue1 15741
{
	//$Title Disciple Statue Marble
	
		States
		{
			Spawn:
				DISS B -1
				Stop
		}
}

ACTOR DiscipleStatue3 : DiscipleStatue1 15742
{
	//$Title Disciple Statue Swamp
	
		States
		{
			Spawn:
				DISS C -1
				Stop
		}
}

ACTOR DiscipleStatue4 : DiscipleStatue1 15743
{
	//$Title Disciple Statue Ice
	
		States
		{
			Spawn:
				DISS D -1
				Stop
		}
}

ACTOR DiscipleStatue5 : DiscipleStatue1 15744
{
	//$Title Disciple Statue Fire
	
		States
		{
			Spawn:
				DISS E -1
				Stop
		}
}

ACTOR DiscipleStatue6 : DiscipleStatue1 15745
{
	//$Title Disciple Statue Rust
	
		States
		{
			Spawn:
				DISS F -1
				Stop
		}
}

ACTOR DiscipleStatue7 : DiscipleStatue1 15746
{
	//$Title Disciple Statue Red
	
		States
		{
			Spawn:
				DISS G -1
				Stop
		}
}

ACTOR DiscipleStatue8 : DiscipleStatue1 15747
{
	//$Title Disciple Statue Bronze
	
		States
		{
			Spawn:
				DISS H -1
				Stop
		}
}

ACTOR DiscipleStatue9 : DiscipleStatue1 15748
{
	//$Title Disciple Statue Copper
	
		States
		{
			Spawn:
				DISS I -1
				Stop
		}
}

ACTOR DiscipleStatue10 : DiscipleStatue1 15749
{
	//$Title Disciple Statue Gold
	
		States
		{
			Spawn:
				DISS J -1
				Stop
		}
}

//Place the RainDropSpawner on your map, and use values set on the specials tab to modify it

//The first argument controls it's area. 128, for example makes it a 256x256 square or 256-radius circle (See fourth argument)
//The second argument controls it's frequency. The lower the number, the heavier the rainfall
//The third argument controls whether or not it plays it's ambient sound. 0 will play the sound, 1 will not
//The fourth argument controls the area type. 0 is a square, 1 is a circle


